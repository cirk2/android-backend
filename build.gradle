/**
 * Gradle's top-level build file which defines the repositories to pull the dependencies from
 * and the default dependency versions used in most modules.
 *
 * @author Armin Schnabel
 * @author Klemens Muthmann
 * @version 2.2.19
 * @since 1.0.0
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    repositories {
        // Added to find the build tools 3.0.1 dependency below via ./gradlew
        google()
        jcenter()
    }
    dependencies {
        // You need to run the integration-test module as 'Android Instrumented Test' run config
        // There is still an open issue (and two bugs) with the currently used version:
        // - Warning: Variant selection conflicts (https://stackoverflow.com/q/47529737/5815054)
        // - Instant Run Error (https://stackoverflow.com/q/46957873/5815054)
        // Workaround: disable instant run & ignore Gradle Sync Warnings
        classpath 'com.android.tools.build:gradle:3.4.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

ext {
    groupId = 'de.cyface'
    version = "4.0.0-alpha10"

    minSdkVersion = 16
    targetSdkVersion = 28
    compileSdkVersion = 28
    buildToolsVersion = '28.0.3'

    androidxAnnotationVersion = "1.0.0"
    androidxAppCompatVersion = "1.0.2"

    junitVersion = "4.12"
    mockitoVersion = "2.26.0"
    hamcrestVersion = "1.3"
    extJunitVersion = "1.1.0"
    rulesVersion = "1.1.1"
    robolectricVersion = "4.3-alpha-2" // To have ShadowNetworkCapabilities class added in 4.3
    androidxTestCoreVersion = "1.0.0" // as suggested in http://robolectric.org/migrating/#migrating-to-40

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
}

group = ext.groupId

allprojects {
    repositories {
        // Needed to resolve newest support libraries with IntelliJ / Gradle
        google()
        jcenter()
    }

    // Uncomment this for additional warnings.
    /*gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }*/
}

apply plugin: 'android-reporting'

configure(subprojects.findAll({ it.name in ["persistence", "synchronization", "datacapturing", "utils"] })) {
    pr ->
        apply plugin: 'com.android.library'
        apply plugin: 'maven-publish'

        group = rootProject.group

        publish.dependsOn 'assemble'

        publishing.publications {
            pr.android.libraryVariants.all { variant ->
                def flavored = !variant.flavorName.isEmpty()
                def variantArtifactId = flavored ? variant.flavorName.replace('_', '-') : project.name

                def javaDocDestDir = file("${buildDir}/docs/javadoc ${flavored ? variantArtifactId : ""}")

                def sourceDirs = variant.sourceSets.collect {
                    it.javaDirectories // Also includes kotlin sources if any.
                }
                def javadoc = task("${variant.name}Javadoc", type: Javadoc) {
                    description "Generates Javadoc for ${variant.name}."
                    source = variant.javaCompileProvider.get().source // Yes, javaCompile is deprecated,
                    // but I didn't find any working alternative. Please, tweet @Louis_CAD if you find one.
                    destinationDir = javaDocDestDir
                    classpath += files(android.getBootClasspath().join(File.pathSeparator))
                    classpath += files(configurations.compile)
                    options.links("http://docs.oracle.com/javase/7/docs/api/")
                    options.links("http://d.android.com/reference/")
                    exclude '**/BuildConfig.java'
                    exclude '**/R.java'
                    failOnError false
                }
                def javadocJar = task("${variant.name}JavadocJar", type: Jar, dependsOn: javadoc) {
                    description "Puts Javadoc for ${variant.name} in a jar."
                    classifier = 'javadoc'
                    from javadoc.destinationDir
                }
                def sourcesJar = task("${variant.name}SourcesJar", type: Jar) {
                    description "Puts sources for ${variant.name} in a jar."
                    from sourceDirs
                    classifier = 'sources'
                }

                "${project.name}${variant.name.capitalize()}"(MavenPublication) {
                    groupId project.group
                    artifactId "${project.name}${variantArtifactId.capitalize()}"
                    version android.defaultConfig.versionName
                    artifact(variant.packageLibraryProvider.get().archivePath)
                    artifact(javadocJar)
                    artifact(sourcesJar)
                }
            }
        }
        repositories {
            mavenLocal()
        }
}

task publishAll() {
    subprojects.each { pr ->
        dependsOn {
            pr.tasks.findAll { task -> task.name.startsWith('publish') }
        }
    }
    outputs.upToDateWhen { false }
}
publishAll.outputs.upToDateWhen { false }

// Auto-generated by Android Studio
task clean(type: Delete) {
    delete rootProject.buildDir
}
